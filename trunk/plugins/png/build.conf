# here, we have to define some usefull defines, so inside C++ code we can check options with #ifdef / #ifndef constructs

contains(PNG_PLUGIN_OPTIONS, READ) {
    DEFINES += _PNG_READ
}

contains(PNG_PLUGIN_OPTIONS, WRITE) {
    DEFINES += _PNG_WRITE
}

!contains(PNG_PLUGIN_OPTIONS, WRITE) {
    !contains(PNG_PLUGIN_OPTIONS, READ) {
         DEFINES += _PNG_READ _PNG_WRITE
    }
}

contains(DEFINES, _PNG_PLUGIN) {
    # default is internal    
    DEFINES += _PNG_PLUGIN_INTERNAL
    DEFINES -= _PNG_PLUGIN_EXTERNAL

    contains(PNG_PLUGIN_OPTIONS, EXTERNAL) {
        DEFINES -= _PNG_PLUGIN_INTERNAL
        DEFINES += _PNG_PLUGIN_EXTERNAL
    }
}

INCLUDEPATH += $$(AMANITHDIR)/3rdpart/zlib

unix: LIBS += $$(AMANITHDIR)/lib/libpng.a $$(AMANITHDIR)/lib/libzlib.a
win32: {

    !contains(DEFINES, WIN32_MINGW) {
        LIBS += $$(AMANITHDIR)/lib/libpng.lib $$(AMANITHDIR)/lib/libzlib.lib
    }

    # Windows MinGW support
    contains(DEFINES, WIN32_MINGW) {
        LIBS += $$(AMANITHDIR)/lib/libpng.a $$(AMANITHDIR)/lib/libzlib.a
    }
}
