#*********************************************************
#
#    LIBRARY SETTINGS, PLEASE SET YOUR DESIRED OPTIONS
#
#*********************************************************
# build mode: it can be 'static' or 'dynamic', in
# combination with 'release' and 'debug'
#*********************************************************

    BUILD_MODE = dynamic release

    # please uncomment the following line if you are buildin Amanith unde MinGW (Windows)
    # DEFINES += WIN32_MINGW

    # please uncomment the following line if you are building Amanith with QT4
    # DEFINES += USE_QT4

#*********************************************************



#*********************************************************
#         BASIC REAL TYPE USED IN ALL CALCULATIONS
#*********************************************************

    # options supported are DOUBLE and FLOAT
    REAL_TYPE = DOUBLE


# [BEGIN PLUGINS]

#*********************************************************
#                OPENGL EXTENSIONS PLUGIN
#*********************************************************

    DEFINES += _OPENGLEXT_PLUGIN

#*********************************************************
#                       JPEG PLUGIN
#*********************************************************

    DEFINES += _JPEG_PLUGIN

    # link options: EXTERNAL / INTERNAL to specify a shared library or static linking inside Amanith library
    # features options: READ | WRITE to specify if plugin can read and/or write jpeg files
    JPEG_PLUGIN_OPTIONS = EXTERNAL READ WRITE

#*********************************************************
#                       PNG PLUGIN
#*********************************************************

    DEFINES += _PNG_PLUGIN

    # link options: EXTERNAL / INTERNAL to specify a shared library or static linking inside Amanith library
    # features options: READ | WRITE to specify if plugin can read and/or write png files
    PNG_PLUGIN_OPTIONS = EXTERNAL READ WRITE

#*********************************************************
#                      FONTS PLUGIN
#*********************************************************

    DEFINES += _FONTS_PLUGIN
    # link options: EXTERNAL / INTERNAL to specify a shared library or static linking inside Amanith library
    # features options: TRUETYPE | TYPE1 | TYPE42 | CFF | PFR | CID to specify witch type of fonts can be read.
    FONTS_PLUGIN_OPTIONS = EXTERNAL TRUETYPE TYPE1 TYPE42 CFF PFR CID


#*********************************************************
# [END PLUGINS]





#*********************************************************
#
#          PLEASE DO NOT TOUCH FOLLOWING LINES!!!
#
#*********************************************************


# little patch to make sure that a build mode is always specified
!contains(BUILD_MODE, dynamic) {
    !contains(BUILD_MODE, static) {
        BUILD_MODE += dynamic
    }
}

#*********************************************************
# 'debug' remove is NECESSARY for release operation, due
# to .qtwinconfig that force debug
#*********************************************************

    CONFIG -= debug
    CONFIG += warn_on

#*********************************************************
# based on build mode specified, we set correct flags
#*********************************************************

    CONFIG += release
    DEFINES -= _DEBUG
    contains(BUILD_MODE, debug) {
        CONFIG -= release
        CONFIG += debug
        DEFINES += _DEBUG
    }

#*********************************************************
#      basic real type, used for all calculations
#*********************************************************

DEFINES -= DOUBLE_REAL_TYPE
DEFINES += FLOAT_REAL_TYPE
contains(REAL_TYPE, DOUBLE) {
    DEFINES += DOUBLE_REAL_TYPE
    DEFINES -= FLOAT_REAL_TYPE
}

#*********************************************************
